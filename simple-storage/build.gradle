apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

// javadoc currently breaks with kotlin files and bintray
tasks.withType(Javadoc) {
	enabled = false
}

ext {
	bintrayRepo = 'maven'
	bintrayName = 'simple-storage'

	publishedGroupId = 'org.neidhardt'
	libraryName = 'simple-storage'
	artifact = 'simple-storage'

	libraryDescription = 'simple android library to store data'

	siteUrl = 'https://github.com/CombatMage/simple-storage'
	gitUrl = 'https://github.com/CombatMage/simple-storage.git'

	libraryVersion = '1.2.0'

	developerId = 'combatmage'
	developerName = 'Eric Neidhardt'
	developerEmail = 'eric.neidhardt@gmail.com'

	licenseName = 'MIT license'
	licenseUrl = 'https://opensource.org/licenses/MIT'
	allLicenses = ["MIT license"]
}

android {
	compileSdkVersion rootProject.ext.compileSDK
	buildToolsVersion rootProject.ext.buildTools

	defaultConfig {
		minSdkVersion project.projectMinSDK
		targetSdkVersion project.projectTargetSDK
	}
	buildTypes {
		release {
			minifyEnabled false
		}
	}
	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
		test.java.srcDirs += 'src/test/kotlin'
		debug.java.srcDirs += 'src/debug/kotlin'
		release.java.srcDirs += 'src/release/kotlin'
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	// kotlin
	implementation rootProject.ext.libraries.kotlin

	// rx
	implementation rootProject.ext.libraries.supportAppCompat
	implementation rootProject.ext.libraries.rxAndroid
	implementation rootProject.ext.libraries.rxKotlin
	implementation rootProject.ext.libraries.rxJava

	// misc
	implementation rootProject.ext.libraries.gson

	// testing
	testImplementation rootProject.ext.libraries.junit
	testImplementation rootProject.ext.libraries.mockito
	testImplementation rootProject.ext.libraries.robolectric
	testImplementation rootProject.ext.libraries.robolectricMultidex
	testImplementation rootProject.ext.libraries.robolectricSupportV4
}

apply plugin: 'com.github.dcendents.android-maven'

group = publishedGroupId // Maven Group ID for the artifact

// Bintray section
// make sure that you have the following in your local.properties
// bintray.user=<your user name>
// bintray.apikey=<your api key
apply plugin: 'com.jfrog.bintray'

version = libraryVersion

if (project.hasProperty("android")) { // Android libraries
	task sourcesJar(type: Jar) {
		classifier = 'sources'
		from android.sourceSets.main.java.srcDirs
	}

	task javadoc(type: Javadoc) {
		source = android.sourceSets.main.java.srcDirs
		classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	}
} else { // Java libraries
	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
	pom {
		project {
			packaging 'aar'
			groupId publishedGroupId
			artifactId artifact

			// Add your description here
			name libraryName
			description libraryDescription
			url siteUrl

			// Set your license
			licenses {
				license {
					name licenseName
					url licenseUrl
				}
			}
			developers {
				developer {
					id developerId
					name developerName
					email developerEmail
				}
			}
			scm {
				connection gitUrl
				developerConnection gitUrl
				url siteUrl

			}
		}
	}.writeTo("$buildDir/poms/pom-default.xml")

	user = properties.getProperty("bintray.user")
	key = properties.getProperty("bintray.apikey")

	configurations = ['archives']
	pkg {
		repo = bintrayRepo
		name = bintrayName
		desc = libraryDescription
		websiteUrl = siteUrl
		vcsUrl = gitUrl
		licenses = allLicenses
		publish = true
		publicDownloadNumbers = true
		version {
			desc = libraryDescription
			gpg {
				// optional GPG encryption. Default is false.
				sign = false
				//passphrase = properties.getProperty("bintray.gpg.password")
			}
		}
	}
}
